load("//toolchains/sysimage:toolchain.bzl", "ext4_image")

package(default_visibility = ["__subpackages__"])

exports_files([
    "partitions.csv",
    "rootfs/docker-base.dev",
    "rootfs/docker-base.prod",
    "rootfs/docker-base.dev-sev",
    "rootfs/allow_console_root",
    "rootfs/extra_boot_args.template",
    "rootfs/dev-certs/canister_http_test_ca.cert",
    "rootfs/dev-certs/canister_http_test_ca.key",
    "rootfs/opt/ic/share/ic.json5.template",
])

filegroup(
    name = "rootfs-files",
    srcs = glob(["rootfs/**"]),
)

ext4_image(
    name = "partition-config.tar",
    partition_size = "100M",
    target_compatible_with = [
        "@platforms//os:linux",
    ],
)

sh_test(
    name = "validate-ic-json5",
    srcs = ["rootfs/opt/ic/bin/validate-replica-config.sh"],
    args = ["$(execpath :rootfs/opt/ic/share/ic.json5.template)"],
    data = [":rootfs/opt/ic/share/ic.json5.template"],
)

genrule(
    name = "launch-single-vm",
    srcs = [
        "//rs/ic_os/launch-single-vm",
        "//ic-os/guestos/envs/dev:disk-img-url",
        "//ic-os/guestos/envs/dev:disk-img.tar.zst.sha256",
        "//ic-os:scripts/build-bootstrap-config-image.sh",
        "//bazel:version.txt",
    ],
    outs = ["launch_single_vm_script"],
    cmd = """
BIN="$(location //rs/ic_os/launch-single-vm:launch-single-vm)"
VERSION="$$(cat $(location //bazel:version.txt))"
URL="$$(cat $(location //ic-os/guestos/envs/dev:disk-img-url))"
SHA="$$(cat $(location //ic-os/guestos/envs/dev:disk-img.tar.zst.sha256))"
SCRIPT="$(location //ic-os:scripts/build-bootstrap-config-image.sh)"
cat <<EOF > $@
#!/usr/bin/env bash
set -euo pipefail
cd "\\$$BUILD_WORKSPACE_DIRECTORY"
$$BIN --version "$$VERSION" --url "$$URL" --sha256 "$$SHA" --build-bootstrap-script "$$SCRIPT"
EOF
    """,
    executable = True,
    tags = ["manual"],
)

# All guest-os targets are named the same, just stay in different submodules.
# To build or run specific target:
#
# bazel run   //ic-os/guestos/envs/prod:upload_disk-img           [--s3_endpoint=<internal endpoint address>]
# bazel build //ic-os/guestos/envs/dev/...
# bazel build //ic-os/guestos/envs/dev-sev/...
#
# check //ic-os/defs.bzl for the full list of targets.
