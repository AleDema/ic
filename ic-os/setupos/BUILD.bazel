load("//toolchains/sysimage:toolchain.bzl", "ext4_image", "fat32_image")
load("@bazel_tools//tools/build_defs/pkg:pkg.bzl", "pkg_tar")
load("@bazel_skylib//rules:copy_file.bzl", "copy_file")

package(default_visibility = ["__subpackages__"])

exports_files([
    "partitions.csv",
    "rootfs/docker-base.dev",
    "rootfs/docker-base.prod",
    "config/config.ini",
    "config/ssh_authorized_keys",
    "data/deployment.json.template",
    "data/nns_public_key.pem",
    "data/host-os.img.tar.gz",
])

filegroup(
    name = "rootfs-files",
    srcs = glob(["rootfs/**"]),
)

pkg_tar(
    name = "config_tar",
    srcs = [
        ":config/config.ini",
        ":config/ssh_authorized_keys",
    ],
    mode = "0644",
    package_dir = "config",
)

genrule(
    name = "deployment_json",
    srcs = [":data/deployment.json.template"],
    outs = ["deployment.json"],
    cmd = "sed -e 's/.*/&/' < $< > $@",
)

copy_file(
    name = "copy_guestos_img",
    src = "//ic-os/guestos/prod:disk-img.tar.gz",
    out = "guest-os.img.tar.gz",
    allow_symlink = True,
)

pkg_tar(
    name = "data_tar",
    srcs = [
        ":data/host-os.img.tar.gz",
        ":data/nns_public_key.pem",
        ":deployment.json",
        ":guest-os.img.tar.gz",
    ],
    mode = "0644",
    package_dir = "data",
)

fat32_image(
    name = "partition-config.tar",
    src = "config_tar",
    label = "CONFIG",
    partition_size = "50M",
    subdir = "./config",
    target_compatible_with = [
        "@platforms//os:linux",
    ],
)

ext4_image(
    name = "partition-data.tar",
    src = "data_tar",
    partition_size = "1750M",
    subdir = "./data",
    target_compatible_with = [
        "@platforms//os:linux",
    ],
)

# All setup-os targets are named the same, just stay in different submodules.
# To build or run specific target:
#
# bazel run   //ic-os/setupos/envs/prod:upload_disk-img           [--s3_endpoint=<internal endpoint address>]
# bazel build //ic-os/setupos/envs/dev/...
#
# check //ic-os/defs.bzl for the full list of targets.
