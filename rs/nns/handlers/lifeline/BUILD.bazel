load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_library")
load("//bazel:canisters.bzl", "optimized_canister")

package(default_visibility = ["//visibility:public"])

genrule(
    name = "lifeline_actor_raw",
    srcs = [
        "lifeline.mo",
        "//rs/nns/governance:canister/governance.did",
        "//rs/nns/handlers/root:canister/root.did",
    ],
    outs = ["lifeline.wasm"],
    cmd_bash = """
    set -e
    mkdir idl
    cat $(location //rs/nns/governance:canister/governance.did) | sed 's|service : (Governance) -> |service : |g' > idl/rrkah-fqaaa-aaaaa-aaaaq-cai.did
    cp $(location //rs/nns/handlers/root:canister/root.did) idl/r7inp-6aaaa-aaaaa-aaabq-cai.did
    $(location @build_bazel_rules_motoko_toolchain//:moc) --actor-idl idl \
      --actor-alias governance rrkah-fqaaa-aaaaa-aaaaq-cai \
      --actor-alias root r7inp-6aaaa-aaaaa-aaabq-cai \
      -o lifeline.wasm \
      -c $(location lifeline.mo)
    cp lifeline.wasm $@
""",
    exec_tools = ["@build_bazel_rules_motoko_toolchain//:moc"],
)

optimized_canister(
    name = "lifeline_actor",
    wasm = ":lifeline_actor_raw",
)

rust_library(
    name = "lifeline",
    srcs = ["src/lib.rs"],
    compile_data = [":lifeline.wasm"],
    edition = "2018",
    rustc_env = {
        "LIFELINE_CANISTER_WASM_PATH": "$(location :lifeline.wasm)",
    },
)

rust_binary(
    name = "lifeline_bin",
    srcs = ["src/main.rs"],
    edition = "2018",
    deps = [":lifeline"],
)
